apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.siziksu.chips"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 2519001
        versionName "0.0.1"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // Disable signing if there is no available keystore (local development)
    def properSigningConfig = android.signingConfigs.debug
    // KeyStore and KeyAlias Password. Uses environment variables to resolve.
    def keyStorePass = "$System.env.KEY_STORE_PASS"
    def keyAliasName = "$System.env.KEY_ALIAS"
    def keyAliasPassword = "$System.env.KEY_ALIAS_PASS"
    // keyStorePath should point to the .jks file
    def keyStorePath = "$System.env.KEY_PATH"
    signingConfigs {
        release {
            storeFile file(keyStorePath)
            storePassword keyStorePass
            keyAlias keyAliasName
            keyPassword keyAliasPassword
        }
    }
    if (keyStorePath != "null") {
        properSigningConfig = signingConfigs.release
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig properSigningConfig
        }
        debug {
            jniDebuggable true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:25.1.0"
    compile "com.android.support:design:25.1.0"
    apt "com.jakewharton:butterknife-compiler:8.2.1"
    compile "com.jakewharton:butterknife:8.2.1"
}
